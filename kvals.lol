HAI 1.3

BTW THIS IS A PANEL OBJECT
O HAI IM PANEL
    I HAS A THIKNISS ITZ 1.0
    I HAS A THOT ITZ 2.0
    I HAS A TCOLD ITZ 1.0
    I HAS A HFM ITZ 1.0
    I HAS A K ITZ 2.0
    I HAS A NAME ITZ "SOME KINDA STUFF"

    HOW DUZ I CONDUCTIVITIZE
        I HAS A C1 ITZ -29.2816
        I HAS A C2 ITZ 0.4471
        I HAS A C3 ITZ 3.5283
        I HAS A C4 ITZ 0.0016
        I HAS A C5 ITZ 0.0007
        I HAS A C6 ITZ 0.0001

        I HAS A DELTAT ITZ DIFF OF THOT AN TCOLD
        I HAS A TAVG ITZ SUM OF THOT AN TCOLD
        TAVG R QUOSHUNT OF TAVG AN 2.0

        I HAS A TERM1 ITZ PRODUKT OF C2 AN TAVG
        TERM1 R SUM OF TERM1 AN C1

        I HAS A TERM2 ITZ PRODUKT OF C4 AN TAVG
        TERM2 R SUM OF TERM2 AN C3
        TERM2 R PRODUKT OF TERM2 AN HFM

        I HAS A TERM3 ITZ PRODUKT OF C6 AN TAVG
        TERM3 R SUM OF TERM3 AN C5
        TERM3 R PRODUKT OF TERM3 AN HFM
        TERM3 R PRODUKT OF TERM3 AN HFM

        I HAS A K ITZ SUM OF TERM1 AN TERM2
        K R SUM OF K AN TERM3
        K R PRODUKT OF K AN THIKNISS
        K R QUOSHUNT OF K AN 1000.0 BTW CONVERTING TO METERS
        K R QUOSHUNT OF K AN DELTAT
    IF U SAY SO

    HOW DUZ I KSI
        CONDUCTIVITIZE
        VISIBLE "CONDUCTIVITY OF "!
        VISIBLE NAME!
        VISIBLE " IN SI UNITS-- "!
        VISIBLE K
    IF U SAY SO

    HOW DUZ I KENG
        CONDUCTIVITIZE
        VISIBLE "CONDUCTIVITY OF "!
        VISIBLE NAME!
        VISIBLE " IN ENGLISH UNITS-- "!
        I HAS A KENG ITZ PRODUKT OF K AN 0.57782 BTW CONVERSION COURTESY CENGEL
        VISIBLE KENG
    IF U SAY SO

    HOW DUZ I RSI
        CONDUCTIVITIZE
        VISIBLE "RESISTIVITY OF "!
        VISIBLE NAME!
        VISIBLE " IN SI UNITS-- "!
        VISIBLE QUOSHUNT OF THIKNISS AN K
    IF U SAY SO

    HOW DUZ I RENG
        CONDUCTIVITIZE
        VISIBLE "RESISTIVITY OF "!
        VISIBLE NAME!
        VISIBLE " IN ENGLISH UNITS-- "!
        I HAS A RENG ITZ QUOSHUNT OF THIKNISS AN K 
        RENG R PRODUKT OF RENG AN 0.52750 BTW CONVERSION COURTESY CENGEL
        VISIBLE RENG
    IF U SAY SO
    
    HOW DUZ I REPORT
        KSI
        RSI
        KENG
        RENG
        VISIBLE ""
    IF U SAY SO
KTHX

I HAS A ALUMPANEL ITZ LIEK A PANEL
I HAS A COMPOSITE ITZ LIEK A PANEL
I HAS A BLUEBORED ITZ LIEK A PANEL

ALUMPANEL!!NAME R "ALUMINUM SANDWICH PANEL"
COMPOSITE!!NAME R "COMPOSITE SANDWICH PANEL"
BLUEBORED!!NAME R "BLUE BOARD FOAM PANEL"

BTW IN MM
ALUMPANEL!!THIKNISS R 32.08
COMPOSITE!!THIKNISS R 29.63
BLUEBORED!!THIKNISS R 37.21

BTW IN CELCIUS
ALUMPANEL!!THOT R 35.8
COMPOSITE!!THOT R 35.5
BLUEBORED!!THOT R 35.5

ALUMPANEL!!TCOLD R 22.6
COMPOSITE!!TCOLD R 20.7
BLUEBORED!!TCOLD R 22.0

BTW ASSUME SI UNITS
ALUMPANEL!!HFM R 27.2
COMPOSITE!!HFM R 19.1
BLUEBORED!!HFM R 14.4


VISIBLE ""
ALUMPANEL!!REPORT
COMPOSITE!!REPORT
BLUEBORED!!REPORT

KTHXBYE
