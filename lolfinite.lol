OBTW
USED MATHIFIED VRSN OF INTERPRETER FROM http://www.icanhaslolcode.org/
NAOW INCLUDES POWR AN LOLG (pow() and log() from math.h)
U CAN FIND @ http://github.com/jesusabdullah/lolcode
ALSO http://lolcode.com/specs/1.2 AN http://lolcode.com/proposals/1.3/bukkit
O AN SOME OBJECT STUFF?? IZ CONFUSING
CRUISE CONTROL ETC
WRITTN BY JOSHUA HOLBROOK
ADOPTED FROM `Computer Code to Calculate h "RODS-415"' FROM ZARLING
LICENSED UNDER THE WTFPL
TLDR

CAN HAS STDIO?
CAN HAS MATH?

HAI 1.3

    HOW DUZ I NUSSELT YR T AN YR TINF AN YR DIAMETER?
        BTW DIS IZ HOW U KAKKALATE H
        I HAS A G ITZ 32.2 BTW FT/S^2
        I HAS A BETA ITZ SUM OF T AN 459.67 BTW CONVERTS TO RANKINE
        BETA R QUOSHUNT OF 1.0 AN BETA
        I HAS A VISC ITZ 0.00021 BTW FT^2/S
        I HAS A KUBEDD ITZ POWR OF DIAMETER AN 3.0
        I HAS A DIFFRINS ITZ DIFF OF T AN TINF
        I HAS A GRASHOF ITZ PRODUKT OF G AN BETA
        GRASHOF R PRODUKT OF GRASHOF AN DIFFRINS
        GRASHOF R PRODUKT OF GRASHOF AN KUBEDD
        GRASHOF R QUOSHUNT OF GRASHOF AN VISC
        GRASHOF R QUOSHUNT OF GRASHOF AN VISC BTW MEANT 2 DO TWICE
        I HAS A PRANDTL ITZ QUOSHUNT OF VISC AN 0.00029 BTW BOTTM IS ALPHA OF AIR
        I HAS A RAYLEE ITZ PRODUKT OF GRASHOF AN PRANDTL

        BTW EQUATION COURTESY CENGEL
        I HAS A TOPPART ITZ POWR OF RAYLEE AN 0.16666667
        TOPPART R PRODUKT OF 0.387 AN TOPPART
        I HAS A BOTTMPART ITZ QUOSHUNT OF 0.559 AN PRANDTL
        BOTTMPART R POWR OF BOTTMPART AN 0.5625
        BOTTMPART R SUM OF BOTTMPART AN 1.0
        BOTTMPART R POWR OF BOTTMPART AN 0.296296296
        I HAS A NUSSELT ITZ QUOSHUNT OF TOPPART AN BOTTMPART
        NUSSELT R SUM OF NUSSELT AN 0.6
        NUSSELT R POWR OF NUSSELT AN 2.0
        FOUND YR NUSSELT
    IF U SAY SO

    OBTW
===========THIS IS WHERE THE FUN STARTS=================
    TLDR

    I HAS A TINF ITZ 72.0 BTW DATS IN FAHRENHEIT
    BTW INISHALIZIN T
    I HAS A T ITZ A BUKKIT BTW STILL IN FAHRENHEIT K?
    T HAS A 0 ITZ 250.0
    T HAS A 1 ITZ 249.0
    T HAS A 2 ITZ 72.0
    T HAS A 3 ITZ 72.0
    T HAS A 4 ITZ 72.0
    T HAS A 5 ITZ 72.0
    T HAS A 6 ITZ 72.0
    T HAS A 7 ITZ 72.0
    T HAS A 8 ITZ 72.0
    T HAS A 9 ITZ 72.0
    T HAS A 10 ITZ 72.0
    T HAS A 11 ITZ 72.0
    T HAS A 12 ITZ TINF

    BTW INISHALIZIN X
    I HAS A X ITZ A BUKKIT BTW INCHEZ NAOW
    X HAS A 0 ITZ -0.01
    X HAS A 1 ITZ 0.0
    X HAS A 2 ITZ QUOSHUNT OF 0.5 AN 12.0
    X HAS A 3 ITZ QUOSHUNT OF 1.5 AN 12.0
    X HAS A 4 ITZ QUOSHUNT OF 3.5 AN 12.0
    X HAS A 5 ITZ QUOSHUNT OF 6.0 AN 12.0
    X HAS A 6 ITZ QUOSHUNT OF 9.0 AN 12.0
    X HAS A 7 ITZ QUOSHUNT OF 12.0 AN 12.0
    X HAS A 8 ITZ QUOSHUNT OF 18.0 AN 12.0
    X HAS A 9 ITZ QUOSHUNT OF 24.0 AN 12.0
    X HAS A 10 ITZ QUOSHUNT OF 30.0 AN 12.0
    X HAS A 11 ITZ QUOSHUNT OF 36.0 AN 12.0
    X HAS A 12 ITZ 20.0 BTW DIS IS "REALLY FAR AWAY" AND NOT REAL

    I HAS A KAIR ITZ 0.01646 BTW BTU/HR/FT/FAHRENHEIT
    I HAS A KAL ITZ 102.3 BTW BTU/HR/FT/FAHRENHEIT
    I HAS A DIAMETER ITZ QUOSHUNT OF 1.0 AN 12.0 BTW DATS IN FT

    I HAS A PIE ITZ 3.14159265
    I HAS A AREA ITZ POWR OF DIAMETER AN 2.0
    AREA R PRODUKT OF AREA AN PIE
    AREA R QUOSHUNT OF AREA AN 4.0
    I HAS A PERIMETR ITZ PRODUKT OF DIAMETER AN PIE


    I HAS A TEMPCHAENG ITZ T!!11 BTW IZ ALSO SOMTIEMZ TOLD
    BTW VISIBLE "--CHANGE IN TEMPERATURE GIVEN ITERATION--"
    BTW VISIBLE "__N__ __DELTA-T__"
    IM IN YR BASE UPPIN YR N TIL BOTH SAEM N AN 1 BTW 10 TOTALLY ARBITRARY
        VISIBLE "  "!
        VISIBLE N!
        VISIBLE "      "!


        I HAS A HPOVERKA
        I HAS A JPLUSWON
        I HAS A JPLUSTOO
        I HAS A DELXPLUS
        I HAS A DELXMIN
        I HAS A DELXAVG
        I HAS A TMINTINF
        I HAS A DELTPLUS
        I HAS A DELTMIN
        I HAS A THING
        I HAS A NUTHERTHING
        IM IN YR LOOP UPPIN YR J TIL BOTH SAEM J AN 9
            JPLUSWON R SUM OF J AN 1
            JPLUSTOO R SUM OF JPLUSWON AN 1

            DELTMIN R DIFF OF T!?JPLUSWON AN T!?J
            DELXMIN R DIFF OF X!?JPLUSWON AN X!?J
            DELXPLUS R DIFF OF X!?JPLUSTOO AN X!?JPLUSWON
            DELXAVG R SUM OF DELXMIN AN DELXPLUS
            DELXAVG R QUOSHUNT OF DELXAVG AN 2.0

            HPOVERKA R NUSSELT T!?JPLUSWON AN TINF AN DIAMETER

            OBTW DEBUGGING
            VISIBLE ""

            VISIBLE "Nu="!
            VISIBLE HPOVERKA
            TLDR            

            HPOVERKA R PRODUKT OF HPOVERKA AN KAIR
            HPOVERKA R QUOSHUNT OF HPOVERKA AN DIAMETER

            OBTW DEBUGGING
            VISIBLE "h="!
            VISIBLE HPOVERKA
            TLDR

            HPOVERKA R PRODUKT OF HPOVERKA AN PERIMETR
            HPOVERKA R QUOSHUNT OF HPOVERKA AN KAL
            HPOVERKA R QUOSHUNT OF HPOVERKA AN AREA
            TMINTINF R DIFF OF T!?JPLUSWON AN TINF
            THING R PRODUKT OF HPOVERKA AN DELXAVG
            THING R PRODUKT OF THING AN TMINTINF

            NUTHERTHING R QUOSHUNT OF DELTMIN AN DELXMIN

            OBTW DEBUGGING
            VISIBLE ""
            VISIBLE "hp/ka delXavg T-Tinf="!
            VISIBLE THING
            VISIBLE "dT/dX="!
            VISIBLE NUTHERTHING
            TLDR


            DELTPLUS R SUM OF THING AN NUTHERTHING
            DELTPLUS R PRODUKT OF DELTPLUS AN DELXPLUS

            OBTW DEBUGGING
            VISIBLE ""
            VISIBLE "Nu="!
            VISIBLE NUSSELT T!?JPLUSWON AN TINF AN DIAMETER
            VISIBLE "dT-="!
            VISIBLE DELTMIN
            VISIBLE "dX-="!
            VISIBLE DELXMIN
            VISIBLE "dT+="!
            VISIBLE DELTPLUS
            VISIBLE "dX+="!
            VISIBLE DELXPLUS
            VISIBLE "dXavg="!
            VISIBLE DELXAVG
            VISIBLE "T-Tinf="!
            VISIBLE TMINTINF
            TLDR

            T!?JPLUSTOO R SUM OF DELTPLUS AN T!?JPLUSWON
        IM OUTTA UR LOOP
        T!!12 R TINF
        TEMPCHAENG R DIFF OF T!!11 AN TEMPCHAENG
        BTW VISIBLE TEMPCHAENG
        TEMPCHAENG R T!!11
    IM OUTTA UR BASE

    VISIBLE " "
    VISIBLE " "
    VISIBLE "--STEADY-STATE TEMPERATURES ALONG ROD--"
    VISIBLE "__X__ __T__"
    IM IN YR LOOP UPPIN YR J TIL BOTH SAEM J AN 11
        VISIBLE "  "!
        VISIBLE X!?J!
        VISIBLE "  "!
        VISIBLE T!?J
    IM OUTTA UR LOOP

KTHXBYE
